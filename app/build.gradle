apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.myinstapaper"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode gitVersionCode()
        versionName gitVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias 'android-release-key'
            keyPassword 'nexus6'
            storePassword 'nexus6'
            storeFile file('../keystore/release.keystore')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"
            versionNameSuffix "-dev"
        }
        prod {
            dimension "version"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // Snackbar
    implementation "com.android.support:design:28.0.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

def gitVersionName() {
    // versionName in "MAJOR.MINOR-COMMIT-gHASH" format
    def versionValues = "git describe --tags".execute().text.split("-")
    if (versionValues.size() == 3)
        return versionValues[0] + '.' + versionValues[1]
    else
        return versionValues[0] + '.0'
}

def gitVersionCode() {
    def versionValues = "git describe --tags".execute().text.split("-")
    def (major, minor) = versionValues[0].tokenize("\\.")
    if (versionValues.size() == 3)
        return major.toInteger() * 10000 +
                minor.toInteger() * 1000 +
                versionValues[1].toInteger() * 100
    else
        return major.toInteger() * 10000 +
                minor.toInteger() * 1000
}

task printVersion() {
    println("Version Name: ${gitVersionName()}")
    println("Version Code: ${gitVersionCode()}")
}
